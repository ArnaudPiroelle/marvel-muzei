apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def getBuildNumber() {
    return System.getenv("CIRCLE_BUILD_NUM") as Integer ?: 0
}

def getKeystorePassword() {
    return System.getenv("APP_KEYSTORE_PASSWORD") as String ?: "PASSWORD"
}

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 22

        applicationId "com.arnaudpiroelle.muzei.marvel"
        versionCode getBuildNumber()
        versionName '1.2.1'
    }

    signingConfigs {
        release {
            storeFile file('marvel-muzei.keystore')
            storePassword "${getKeystorePassword()}"
            keyAlias 'marvel-muzei'
            keyPassword "${getKeystorePassword()}"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt')
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        disable 'InvalidPackage'
        enable 'NewerVersionAvailable'
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    /* Support libs */
    compile 'com.android.support:appcompat-v7:22.2.0'

    /* Muzei API */
    compile 'com.google.android.apps.muzei:muzei-api:2.0'

    /* Retrofit */
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'

    /* Dependencies injection */
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'

    compile 'com.jakewharton:butterknife:6.1.0'

    /* Monitoring */
    compile('com.crashlytics.sdk.android:crashlytics:2.4.0@aar') {
        transitive = true;
    }

    /* Play Services */
    compile 'com.google.android.gms:play-services-base:7.5.0'
    compile 'com.google.android.gms:play-services-analytics:7.5.0'

    compile 'io.reactivex:rxandroid:0.24.0'

    testCompile 'junit:junit:4.12'
}
